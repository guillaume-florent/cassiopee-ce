import distutils.sysconfig, os
import KCore.Dist as Dist
#==============================================================================
# RigidMotion requires:
# C++ compiler
# Numpy
# KCore
#
# Optional motion from solvers requires:
# Cassiopee/Kernel
# elsA/Kernel
#==============================================================================

# Get prefix from command line
prefix = ARGUMENTS.get('prefix', '')
installPath = Dist.getInstallPath(prefix)

# Get compilers from Distutils ================================================
(cc, cxx, opt, basecflags, ccshared, ldshared, so_ext) = Dist.getDistUtilsCompilers()

# Python ======================================================================
(pythonVersion, pythonIncDir, pythonLibDir, pythonLibs) = Dist.checkPython()

# Test if numpy exists =======================================================
(numpyVersion, numpyIncDir, numpyLibDir) = Dist.checkNumpy()

# Test if kcore exists =======================================================
(kcoreVersion, kcoreIncDir, kcoreLibDir) = Dist.checkKCore()

# Setting libraryDirs and libraries ===========================================
libraryDirs = [pythonLibDir, kcoreLibDir]
libraries = ["kcore"]
from KCore.config import *
(ok, libs, paths) = Dist.checkCppLibs([], additionalLibPaths)
libraryDirs += paths; libraries += libs

# Env =========================================================================
import srcs
if Dist.getSystem()[0] == 'mingw': tools = ['mingw']
else: tools = None
AR = Dist.getAR()
env = Environment(tools=tools, CC=cc, CXX=cxx, AR=AR, CPPFLAGS=[],
                  LIBPATH=additionalLibPaths+libraryDirs,
                  LIBS=libraries+additionalLibs,
                  CPPPATH=["RigidMotion"]+additionalIncludePaths+[pythonIncDir, numpyIncDir, kcoreIncDir],
                  SHLIBSUFFIX=so_ext, SHLIBPREFIX='',
                  LINKFLAGS=Dist.getLinkArgs(),
                  ENV=Dist.getEnvForScons())
env.Replace(CFLAGS=Dist.getCArgs())
env.Replace(CXXFLAGS=Dist.getCppArgs())

#==============================================================================

myMod = env.StaticLibrary('rigidMotion', srcs.cpp_srcs)
modDir = installPath+'/RigidMotion/'
dp1 = env.Install(modDir, myMod)
dp2 = env.Install(modDir, Glob('RigidMotion/*.py'))

env.Alias(target="install", source=[dp1,dp2])
